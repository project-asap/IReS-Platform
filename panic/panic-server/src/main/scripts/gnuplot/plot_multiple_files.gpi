#!/usr/bin/gnuplot
# this file takes many files as input and produces a single graph
# each file is depicted using 1st column as X and COL_PRINT column 
# as Y


# environment variables section
INPUT_FILES = system("echo $INPUT_FILES")
COL_PRINT = system("echo $COL_PRINT")
TITLE = system("echo $TITLE")
X_LABEL = system("echo $X_LABEL")
Y_LABEL = system("echo $Y_LABEL")
INDEX = system("echo $INDEX")

if(TITLE eq "") TITLE = "Sample Graph"
if(X_LABEL eq "")X_LABEL = "X axis" 
if(Y_LABEL eq "")Y_LABEL = "Y axis" 
if(INDEX eq "")INDEX = "1"


if(INPUT_FILES eq "") print "No INPUT_FILES set" ; exit 
if(COL_PRINT eq "") print "No COL_PRINT set" ; exit 

set macro
print "INPUT_FILES:\t".INPUT_FILES
print "COL_PRINT:\t".COL_PRINT
set grid
set xlabel X_LABEL
set ylabel Y_LABEL
set title TITLE

plot for [filename in INPUT_FILES] filename index @INDEX using @COL_PRINT with lines title filename
pause -1

